// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.coroutine_version = '1.3.3'
    ext.daggerVer = '2.24'
    ext.recyclerview_version = '1.1.0'
    ext.appcompat_veriosn = '1.1.0'
    ext.navigation_version = '2.2.1'
    ext.preference_version = '1.1.0'
    ext.retrofit_version = '2.7.1'
    ext.jacoco_version = "0.7.7.201606060606"
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
    }
}


subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 28
            versionName "0.7"
            versionCode 10

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        flavorDimensions "store"
        productFlavors {
            googlePlay {
                dimension "store"
            }
            nonGooglePlay {
                dimension "store"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        dataBinding {
            enabled = true
        }

        lintOptions {
            abortOnError true
        }

        testOptions {
            unitTests {
                returnDefaultValues = true

                all {
                    maxHeapSize = "1024m"
                }
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        testImplementation 'junit:junit:4.12'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
