// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.kotlin.gradle
        classpath 'com.google.gms:google-services:4.4.2'
        classpath libs.navigation.gradle
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
    }
}


subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 34

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 34
            versionName "0.14"
            versionCode 17

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        flavorDimensions "store"
        productFlavors {
            googlePlay {
                dimension "store"
            }
            nonGooglePlay {
                dimension "store"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = '17'
        }

        buildFeatures {
            dataBinding true
        }

        lintOptions {
            abortOnError true
        }

        testOptions {
            unitTests {
                returnDefaultValues = true

                all {
                    maxHeapSize = "1024m"
                }
            }
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
