// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.10'
    ext.coroutine_version = '1.7.3'
    ext.daggerVer = '2.48'
    ext.recyclerview_version = '1.3.1'
    ext.appcompat_veriosn = '1.6.1'
    ext.navigation_version = '2.5.3'
    ext.preference_version = '1.2.1'
    ext.retrofit_version = '2.9.0'
    ext.jacoco_version = "0.8.8"
    ext.assertj_version = "3.24.2"
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.15'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'
    }
}


subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 33

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 33
            versionName "0.10"
            versionCode 13

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        flavorDimensions "store"
        productFlavors {
            googlePlay {
                dimension "store"
            }
            nonGooglePlay {
                dimension "store"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = '17'
        }

        buildFeatures {
            dataBinding true
        }

        lintOptions {
            abortOnError true
        }

        testOptions {
            unitTests {
                returnDefaultValues = true

                all {
                    maxHeapSize = "1024m"
                }
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        testImplementation 'junit:junit:4.13.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
