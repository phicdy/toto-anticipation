// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.32'
    ext.coroutine_version = '1.5.2'
    ext.daggerVer = '2.42'
    ext.recyclerview_version = '1.2.1'
    ext.appcompat_veriosn = '1.3.1'
    ext.navigation_version = '2.4.2'
    ext.preference_version = '1.2.0'
    ext.retrofit_version = '2.9.0'
    ext.jacoco_version = "0.8.8"
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.12'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
    }
}


subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 31

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 31
            versionName "0.8"
            versionCode 11

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        flavorDimensions "store"
        productFlavors {
            googlePlay {
                dimension "store"
            }
            nonGooglePlay {
                dimension "store"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        buildFeatures {
            dataBinding true
        }

        lintOptions {
            abortOnError true
        }

        testOptions {
            unitTests {
                returnDefaultValues = true

                all {
                    maxHeapSize = "1024m"
                }
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        testImplementation 'junit:junit:4.13.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
